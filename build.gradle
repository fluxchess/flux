apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

description = 'Flux Chess Engine'

group = 'com.fluxchess'

if (!hasProperty('buildNumber')) {
    ext.buildNumber = 'dev'
}
if (version.contains('-')) {
    if (hasProperty('branchName') && branchName == 'master') {
        version += '.' + buildNumber
    } else {
        version += '.0'
    }
}

if (!hasProperty('revisionNumber')) {
    ext.revisionNumber = 'rev'
}
if (version.contains('-') && !(hasProperty('branchName') && branchName == 'master')) {
    version += '+' + revisionNumber
}

println "Building version ${version}"

repositories {
    mavenCentral()
    maven {
        url "http://maven.fluxchess.com/release"
    }
}

configurations {
    eclipselibs {
        extendsFrom compile
    }
}

task libs(type: Sync) {
    from configurations.eclipselibs
    into "$projectDir/lib"
}

tasks.withType(Pmd) {
    pmd {
        ignoreFailures = true
    }
}

tasks.withType(FindBugs) {
    findbugs {
        ignoreFailures = true
    }
}

tasks.withType(Checkstyle) {
    checkstyle {
        ignoreFailures = true
        showViolations = false
    }
}

dependencies {
    compile 'com.fluxchess:jcpi:0.3.+'

    testCompile 'junit:junit:4.+'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    from {
        configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}
    }
    from sourceSets.main.allJava
    manifest {
        attributes 'Implementation-Title': project.group, 'Implementation-Version': project.version, 'Main-Class': 'com.fluxchess.flux.Flux'
    }
}

task dist(type: Zip) {
    def baseDir = "${project.name}-${project.version}"

    into ("$baseDir") {
        from 'README.md'
        from 'COPYING'
        from 'COPYING.LESSER'
        from 'src/dist/Flux.bat'
        from ('src/dist/Flux') {
            fileMode = 0755
        }
        from jar
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
